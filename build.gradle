group 'org.motechproject.mots'
version '0.1-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.7.RELEASE")
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: "com.moowork.node"


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.security.oauth:spring-security-oauth2'
    compile 'mysql:mysql-connector-java'
    compile 'org.projectlombok:lombok:1.16.18'
    compile 'org.hibernate:hibernate-java8'

    testCompile "junit:junit:4.12"
}

processResources {
    expand(project.properties)
}

node {
    version = '6.11.4'
    npmVersion = '3.10.10'
    download = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
}

processResources.dependsOn 'webpack'

checkstyle {
    toolVersion '8.0'
    configFile = file("checkstyle.xml")
    maxWarnings = 0
}

pmd {
    toolVersion = '5.8.1'
    consoleOutput= true
    ignoreFailures = false
    ruleSetFiles = files("ruleset.xml")
    reportsDir = file("build/reports/pmd")
}


clean.delete << file('src/main/resources/static/site')