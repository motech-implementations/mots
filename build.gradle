buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE")
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '1.5.10.RELEASE'
    id 'checkstyle'
    id 'pmd'
    id 'com.moowork.node' version '1.2.0'
    id 'net.ltgt.apt' version '0.8'
    id 'net.researchgate.release' version '2.7.0'
}

group 'org.motechproject.mots'
version '0.1-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/" }
    maven { url "http://jasperreports.sourceforge.net/maven2/" }
}

dependencies {
    compile 'org.springframework:spring-context-support'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-mail'
    compile 'org.springframework.security.oauth:spring-security-oauth2'
    compile 'org.springframework.security:spring-security-jwt'
    compile 'mysql:mysql-connector-java'
    compile 'org.projectlombok:lombok:1.16.18'
    compile 'org.hibernate:hibernate-java8'
    compile 'org.liquibase:liquibase-core:3.5.3'
    compile 'org.mapstruct:mapstruct-jdk8:1.2.0.Final'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.poi:poi-ooxml:3.17'
    compile "net.sf.jasperreports:jasperreports:6.4.1"
    compile 'net.sf.supercsv:super-csv:2.4.0'
    compile 'com.google.guava:guava:24.1-jre'

    apt 'org.mapstruct:mapstruct-processor:1.2.0.Final'
    apt 'org.projectlombok:lombok:1.16.18'

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.+"
    testCompile "org.powermock:powermock-api-mockito:1.6.5"
    testCompile "org.powermock:powermock-module-junit4:1.6.5"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.security:spring-security-test:5.0.3.RELEASE"
}

processResources {
    filesMatching("**/application.yml") {
        expand(project.properties)
    }
}

node {
    version = '6.11.4'
    npmVersion = '3.10.10'
    download = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
}

task webpackWatch(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
    args = ['--watch']
}

task webpackProduction(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
    args = ['-p']
}

jar.dependsOn 'webpackProduction'
bootRun.dependsOn 'webpack'

checkstyle {
    toolVersion '8.0'
    configFile = file("checkstyle.xml")
    maxWarnings = 0
}

pmd {
    toolVersion = '5.8.1'
    consoleOutput= true
    ignoreFailures = false
    ruleSetFiles = files("ruleset.xml")
    reportsDir = file("build/reports/pmd")
}

apply from: 'liquibase.gradle'
